apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'realm-android'

//These variable refer to release builds so make sure they are correct. If you need to override them
//for some specific development needs then use variables that can be passed to gradle on command line.
String releaseVersionName = '0.0.1'
int releaseVersionCode = 1
int releaseMinSdk = 18

android {
    int developProductVersionCode = System.getenv("BUILD_ID") as Integer ?: releaseVersionCode
    String developProductVersionName = rootProject.hasProperty('productVersionName') ?
            rootProject.productVersionName : releaseVersionName
    int developMinSdk = rootProject.hasProperty('productMinSdk') ?
            rootProject.productMinSdk.toInteger() : releaseMinSdk

    compileSdkVersion 26
    buildToolsVersion "26.0.1"

    defaultConfig {
        applicationId "com.thomascook.dreamcatcher"
        minSdkVersion developMinSdk
        targetSdkVersion 26
        versionCode developProductVersionCode
        versionName developProductVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments.notClass = 'com.android.dex.DexIndexOverflowException'
        //Enable to support vector drawables
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    signingConfigs {
        release {
            storeFile file("../Signing/DreamCatcher.jks")
            storePassword 'Kotor4Noway&'
            keyAlias 'Dream Catcher'
            keyPassword 'Kotor4Noway&'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        debug.java.srcDirs += 'src/debug/kotlin'
        release.java.srcDirs += 'src/release/kotlin'
    }

    dataBinding {
        enabled = true
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.release
            dexOptions {
                jumboMode = true
                javaMaxHeapSize "1g"
            }
            minifyEnabled false
            //Used by crashlytics
            ext.alwaysUpdateBuildId = false
            // Disable fabric build ID generation for debug builds
            ext.enableCrashlytics = false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            dexOptions {
                jumboMode = true
                javaMaxHeapSize "1g"
            }
        }
    }

    flavorDimensions "default"

    productFlavors {
        develop {
            applicationIdSuffix ".develop"
            versionNameSuffix "-develop"
            dimension "default"
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/MANIFEST.MF'
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    def material_dialogues_version = "0.9.4.5"
    def androidSupportVersion = "26.0.1" //"25.4.0"
    def guava_version = "23.0-android"
    def constraint_layout_version = "1.0.2"
    def okhttp_version = "3.8.1"
    def retrofitVersion = "2.3.0"
    def stethoVersion = "1.5.0"
    def stetho_realmVersion = "2.0.0"
    def mockitoVersion = '2.8.47'
    def junit_version = "4.12"
    def crashlyticsVersion = "2.6.8"
    def rxKotlin_version = "2.1.0"
    def rxAndroid_version = "2.0.1"
//    def testRulesVersion = "1.0.0"
    def testRunnerVersion = "1.0.0"
    def moshiVersion = "1.5.0"

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "com.android.support:multidex:1.0.2"
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jre7', version: "$kotlin_version"

    kapt group: 'com.android.databinding', name: 'compiler', version: "$gradle_plugin_version"
    kapt "io.realm:realm-annotations:$realm_version"
    kapt "io.realm:realm-annotations-processor:$realm_version"

    implementation "com.android.support:appcompat-v7:$androidSupportVersion"
    implementation "com.android.support:support-v13:$androidSupportVersion"

    implementation group: 'com.android.support.constraint', name: 'constraint-layout', version: "$constraint_layout_version"
    implementation "com.android.support:design:$androidSupportVersion"
    implementation "com.android.support:support-vector-drawable:$androidSupportVersion"
    implementation "com.android.support:animated-vector-drawable:$androidSupportVersion"
    implementation "com.android.support:recyclerview-v7:$androidSupportVersion"
    implementation "com.android.support:cardview-v7:$androidSupportVersion"
    implementation "com.android.support:customtabs:$androidSupportVersion"

    // Guava
    implementation "com.google.guava:guava:$guava_version"

    //Material dialogues
    implementation group: 'com.afollestad.material-dialogs', name: 'commons', version: "$material_dialogues_version"

    implementation("com.crashlytics.sdk.android:crashlytics:$crashlyticsVersion@aar") {
        transitive = true
    }

    //Library for displaying progress spinner
    //https://github.com/81813780/AVLoadingIndicatorView
    implementation 'com.wang.avi:library:2.1.3'


    def thirtyinchVersion = '0.8.0'
    // MVP for activity and fragment
    implementation "net.grandcentrix.thirtyinch:thirtyinch:$thirtyinchVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-vector-drawable'
        exclude group: 'com.android.support', module: 'animated-vector-drawable'
        exclude group: 'io.reactivex', module: 'rxjava'
    }
    implementation("net.grandcentrix.thirtyinch:thirtyinch-rx:$thirtyinchVersion") {
        exclude module: 'rxjava'
    }

    // rx extension
    implementation group: 'io.reactivex.rxjava2', name: 'rxandroid', version: "$rxAndroid_version"
    implementation group: 'io.reactivex.rxjava2', name: 'rxkotlin', version: "$rxKotlin_version"

    //Square OkHttp and retrofit libraries
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: "$okhttp_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation(group: 'com.squareup.retrofit2', name: 'adapter-rxjava2', version: "$retrofitVersion") {
        exclude module: 'okhttp'
        exclude module: 'rxjava'
    }
    implementation "com.squareup.retrofit2:converter-scalars:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    implementation "com.squareup.moshi:moshi-kotlin:$moshiVersion"

    //Add Facebook Stetho
    debugImplementation "com.facebook.stetho:stetho:$stethoVersion"
    debugImplementation "com.facebook.stetho:stetho-okhttp3:$stethoVersion"
    //Realm integration with stetho
    debugImplementation group: 'com.uphyca', name: 'stetho_realm', version: "$stetho_realmVersion"


    testImplementation group: 'junit', name: 'junit', version: "$junit_version"
    testImplementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: "$kotlin_version"
    testImplementation group: 'org.jetbrains.kotlin', name: 'kotlin-test-junit', version: "$kotlin_version"
    testImplementation group: 'org.mockito', name: 'mockito-core', version: "$mockitoVersion"
    testImplementation "org.robolectric:robolectric:3.4.2"
    testImplementation("com.nhaarman:mockito-kotlin:1.5.0") {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-reflect'
    }
    testImplementation "com.squareup.okhttp3:mockwebserver:$okhttp_version"

    androidTestImplementation("com.android.support.test:runner:$testRunnerVersion") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    // JUnit4 Rules
    //androidTestCompile "com.android.support.test:rules:$testRulesVersion"
    androidTestImplementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: "$kotlin_version"
    androidTestImplementation "com.android.support:support-annotations:$androidSupportVersion"
    androidTestImplementation "com.android.support:appcompat-v7:$androidSupportVersion"
    androidTestImplementation group: 'org.mockito', name: 'mockito-android', version: "$mockitoVersion"
    androidTestImplementation("com.nhaarman:mockito-kotlin:1.5.0") {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-reflect'
    }
}